
camera.origin.z = 0.75;

// For rendering
settings.width = 600;
settings.height = 600;

settings.background = F3("222");
settings.max_distance = 4.0;

// For polygonization
settings.grid_size = 0.5;
settings.grid_step_size = 0.002;
settings.iso_value = 0.0001;

let bsdf = BSDF();

bsdf.iterations = 300;
bsdf.depth = 4;

settings.renderer = bsdf;

let light = PointLight();
light.position = F3(5.0, 2.0, 5.0);
light.intensity = 20.0;
light.radius = 2.0;

let light2 = PointLight();
light2.position = F3(-5.0, -2.0, 5.0);
light2.intensity = 20.0;
light2.radius = 2.0;

let light3 = PointLight();
light3.position = F3(0.0, 8.0, 5.0);
light3.intensity = 10.0;
light3.radius = 1.0;

let gold = Material();
gold.rgb = F3(1.0, 0.71, 0.29);//F3("9F6F4A");
gold.metallic = 1.0;
gold.roughness = 0.0;

// Main shape - We make a smooth blend between a sphere and a cone

let sphere = Sphere(0.24);
sphere.material = gold;
let cone = Cone(0.3, 0.25, 0.0);
cone.material = gold;

let helmet = smin(sphere, cone, 0.5);

// Make it hollow by creating a copy, subtract it and move it down a bit
// to open the bottom

let cut_out = helmet.copy();
cut_out.position.y -= 0.04;
cut_out.scale = 0.98;
helmet -= cut_out;

// Eye holes - We mirror an Ellipsoid on the x-axis and subtract it.

let eyes = Ellipsoid();
eyes.size = F3(0.11, 0.03, 0.1);
eyes.position = F3(0.06, -0.03, 0.3);
eyes.mirror.x = true;
helmet -= eyes;

// Nose and mouth - We modify a box and subtract it.

let cut = Box(F3(0.07, 0.2, 0.1));
cut.position.y -= 0.25;
cut.position.z = 0.2;

let modifier = RayModifier("x", "*", "sin", "y");
modifier.frequency = 10.0;
modifier.amplitude = 0.7;
modifier.addend = 1.0;
cut.modifier = modifier;
helmet -= cut;

// Stripe - We add a positive groove in the intersection between
// the helmet and a box.

let stripe = Box(F3(0.011, 0.17, 0.2));
stripe.material.rgb = F3(0.56, 0.57, 0.58);//F3(0.95, 0.93, 0.88);
stripe.material.metallic = 1.0;
stripe.material.roughness = 0.2;

stripe.position.y = 0.16;
stripe.position.z = 0.2;
helmet += Groove(stripe, 0.01, 0.02);
